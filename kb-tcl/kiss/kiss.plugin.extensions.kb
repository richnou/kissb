# SPDX-FileCopyrightText: 2024 KISSB
#
# SPDX-License-Identifier: Apache-2.0


############################
## Modules
##############################
kissb.extension kissb {

    module.path path {

        set path [file normalize $path]
        if {![file exists $path]} {
            log.warn "module.path $path doesn't not exist"
        } elseif {[file exists $path/pkgIndex.tcl]} {
            set dir $path
            source $path/pkgIndex.tcl
        }
        #lappend ::kiss::packages::modulepaths $path
    }

}
############################
## Core extension -> language
##############################

kissb.extension kissb {

    args.contains {v script {else ""} {elseScript {}}} {
        set localArgs [uplevel {::set args}]
        if {[lsearch -exact $localArgs $v]!=-1} {
            uplevel [list eval $script]
        } else {
            uplevel [list eval $elseScript]
        }
    }

    args.ifContains {v script {else ""} {elseScript {}}} {
        set localArgs [uplevel {::set args}]
        if {[lsearch -exact $localArgs $v]!=-1} {
            uplevel [list eval $script]
        } else {
            uplevel [list eval $elseScript]
        }
    }

    args.ifNotContains {v script {else ""} {elseScript {}}} {
        set localArgs [uplevel {::set args}]
        if {[lsearch -exact $localArgs $v]==-1} {
            uplevel [list eval $script]
        } else {
            uplevel [list eval $elseScript]
        }
    }

    args.withValue {v varname script} {
        set args [uplevel {::set args}]
        kissb.args.ifContains $v {
            uplevel [list ::set $varname [lindex $args [expr [lsearch -exact $args $v]+1]]]
            uplevel [list eval $script]
        }
    }

    args.get {v default {to ""} {varname ""}} {
        set localArgs [uplevel {::set args}]

        # Get default or val from args
        set __val $default
        if {[lsearch -exact $localArgs $v]!=-1} {
            set __val [lindex $localArgs [lsearch -exact $localArgs $v]+1]
        }

        # set to requested varname
        if {$to!=""} {
            uplevel [list ::set $varname ${__val}]
        }
        return ${__val}
    }

    args.after {v default {to ""} {varname ""}} {
        set localArgs [uplevel {::set args}]

        # Get default or val from args
        set __val $default
        set __indexOfArg [lsearch -exact $localArgs $v]
        if {${__indexOfArg}!=-1} {
            set __val [lrange $localArgs [expr ${__indexOfArg} +1] end]
        }

        # set to requested varname
        if {$to!=""} {
            uplevel [list ::set $varname ${__val}]
        }
        return ${__val}
    }


    each {lst script} {
        foreach v $lst {
            uplevel [list set it $v]
            uplevel [list eval $script]
        }
    }

}

##############################
## vars extension
##############################
kissb.extension vars {

     ## Define a variable, with documentation, default value also extracted from environment variable
    define {name args} {
        ::kiss::vars::define $name {*}$args
        return [vars.get $name]
    }

    ## Set value for a variable
    set {name args} {
        ::kiss::vars::set $name {*}$args
        return [vars.get $name]
    }

    revert {args} {
        foreach n $args {
            ::kiss::vars::revert $n
        }

    }

    ## This method will set the var if not set
    ensure {name args} {
        if {![vars.exists $name]} {
            return [vars.set $name {*}$args]
        } else {
            return [vars.get $name]
        }
    }

    append {name args} {
        foreach v $args {
             ::kiss::vars::append $name $v
        }
    }

    get {name {v ""}} {
        return [::kiss::vars::get $name $v]
    }

    exists name {
        return [::kiss::vars::exists $name]
    }

    ## Module based variable resolution
    resolve {spec {default ""}} {

        # Split at . MODULE/MODULE/MODULE.VAR.VAR.VAR , and take first result
        set module [lindex [split $spec .] 0]
        set name   [join [lrange [split $spec .] 1 end] .]

        # For a module a/b/c, build search paths: a/b/c.VAR , a/b.VAR then a.VAR   (override effect)
        set i 0
        set candidates {}
        set splitModuleNames [split $module /]
        foreach splitName $splitModuleNames {
            set splitName [lindex [split $splitName .] 0]
            set candidateName [join [lrange $splitModuleNames 0 end-$i] /]
            lappend candidates $candidateName
            incr i
        }
        foreach candidate $candidates {
            #log.info "Searching for var $name in $candidate"
            if {[vars.exists ${candidate}.${name}]} {
                return [vars.get ${candidate}.${name}]
            }
        }

        if {$default==""} {
            return -error "Variable could not be resolved: $spec"
        } else {
            return $default
        }
    }
}

## Standard set extensions
proc ::set.ifEmpty {varName value} {
    set vVal [string trim [uplevel [list set $varName]]]
    if {$vVal eq ""} {
        uplevel [list set $varName $value]
    }
}

##############################
## env extension
##############################
kissb.extension env {

    isDefined name {
        return [expr [llength [array get ::env $name]] > 0 ? true : false]
    }
}

##############################
## Dependencies extension
##############################
kissb.extension dependencies {

    ## Save provided dict in bom
    bom dict {
        #log.info "Adding BOM: $spec"
        ::kiss::dependencies::addBOM $dict
    }

    add {module resolver args} {

        foreach dSpecs $args {
            foreach dSpec $dSpecs {
                log.info "Adding Dependency: $dSpec"
                ::kiss::dependencies::addDepSpec $module $dSpec $resolver
            }
        }

    }

    getDeps {module args} {
        return [::kiss::dependencies::getDeps $module]
    }

    resolve {module type args} {
        return [::kiss::dependencies::resolveDeps $module $type]
    }
}

##############################
## Dependencies extension
##############################
kissb.extension sources {

    ## Save provided dict in bom
    add {module folder} {
        ::kiss::sources::addSourceFolder $module $folder
    }
}

#####################
## Asserts
#####################

kissb.extension assert {

    isFile {path msg} {
        if {![file isfile $path]} {
            error "File doesn't exist:$path , $msg"
        }
    }
}


#####################
## OS
######################

kissb.extension os {

    isWindows  args {
        if {$::tcl_platform(platform)=="windows"} {
            return true
        } else {
            return false
        }
    }
    isWindows64  args {
        if {$::tcl_platform(platform)=="windows" && $::tcl_platform(machine) == "amd64"} {
            return true
        } else {
            return false
        }
    }

    isLinux  args {
        if {$::tcl_platform(platform)=="unix"} {
            return true
        } else {
            return false
        }
    }

    isLinux64  args {
        if {$::tcl_platform(platform)=="unix" && $::tcl_platform(machine) == "x86_64"} {
            return true
        } else {
            return false
        }
    }

    isLinuxWSL  args {
        if {$::tcl_platform(platform)=="unix" && [string match *WSL2* [exec uname -a]]} {
            return true
        } else {
            return false
        }
    }
}
